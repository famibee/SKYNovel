;#USE-FONT	ipam
	[return cond="const.sn.frm.archive"]

	[add_frame id=archive src="archive/_archive.htm" visible=false]

	[event key='dom=archive:#do_loadsave' label=*do_loadsave global=true]
	[event key='dom=archive:#do_del' label=*do_del call=true global=true]
	[event key='dom=archive:#close' label=*exit global=true]
[return]

;*********************************************************
;	TitleMenu -> Load
*title_load
	&しおりモード = "Load"
	&呼び元 = "Title"
[jump label=*main]

;*********************************************************
;	右クリック -> Load
*rclick_load
	[return cond="! isGameState"]

	&しおりモード = "Load"
	&呼び元 = "Text"
[jump label=*main]

;*********************************************************
;	右クリック -> Save
*rclick_save
	[return cond="! isGameState"]

	&しおりモード = "Save"
	&呼び元 = "Text"
[jump label=*main]

;*********************************************************
*main
	[enable_event enabled=false]
	[clear_event]

	; 最新値で更新
	[set_frame id=archive var_name=val_mode text=&しおりモード]
	[set_frame id=archive var_name=val_caller text=&呼び元]
	[set_frame id=archive var_name=val_dic text=&const.sn.bookmark.json]
	[let_frame id=archive var_name=val2ctrl function=true]

	; 下部からスライドイン
	[frame id=archive visible=true y=&const.sn.frm.archive.height]
	[tsy_frame id=archive time=500 y=0]
	[wait_tsy id=archive]

	&isGameState = false
	[event key=rightclick label=*exit]
	[event key=command label=*exit]
	[event key=control label=*exit]
	[event key=end label=*exit]
	[event key=shift label=*exit]
	[event key=c label=*exit]
	[set_focus to=null]
[s]


*do_loadsave
	[let_frame id=archive var_name=val_place]
	[jump label=*save cond="しおりモード=='Save'"]
[jump label=*load]

*do_del
	[let_frame id=archive var_name=val_place]
	[erasebookmark place=&const.sn.frm.archive.val_place]
[return]


;*********************************************************
*save
	&_save.place = const.sn.frm.archive.val_place
	[se fn=&sysse_ok1 buf=SYS]

	[call label=*do_save]
[jump label=*exit]

*do_save_resume
	&_save.place = 0
	&pic = '_archive_resume.jpg'
	[jump label=*do_save_noss]

*do_save
	[let name=pic text="& 'userdata:/storage/'+ _save.place +'.jpg'"]
	[snapshot fn=&pic layer=&dsp_lays smoothing=true cond=const.sn.isApp]
	[let name=pic text='_archive_browser.jpg' cond=!const.sn.isApp]

*do_save_noss
	[enable_event enabled=true]	; この状態も保存されるので本文に戻る前に変更
	[save place=&_save.place pic=&pic dt=&const.Date.getDateStr txt=&const.sn.last_page_text]
[return]

;*****************************
*quick_save
	[return cond="! isGameState"]

	&_save.place = sys:archive.quick_place
	[call label=*do_save]
	[let name="sys:archive.quick_place" text="&(_save.place +1) % const._sl.th_一ページあたりの数 +(const._sl.ページ数 *const._sl.th_一ページあたりの数)"]

	[notice text='クイックセーブしました']
[return]

;*********************************************************
*load
	&_load.place = const.sn.frm.archive.val_place
	[se fn=&sysse_ok2_long buf=SYS]

	[frame id=archive visible=false]

*do_load
	[sys_scenario_start no_resume_save=true]
	[copybookmark from=&_load.place to=0]
	[load place=&_load.place]
;	[load place=&_load.place fn=archive label=*do_load_init]

*do_load_resume
	[se fn=&sysse_ok2_long buf=SYS]
	[notice text='前回終了ポイントから再開します（キャンセル=ESC）']
	[sys_scenario_start no_resume_save=true]
	[load place=0]
;	[load place=0 fn=archive label=*do_load_init]

; ロード後最初にしたい共通処理
*do_load_init

[return]

;*****************************
*quick_load
	[return cond="! isGameState"]
	[return cond="sys:archive.quick_place == null"]
	[let name="_sl.place" text="&(sys:archive.quick_place +const._sl.th_一ページあたりの数 -1) % const._sl.th_一ページあたりの数 +(const._sl.ページ数 *const._sl.th_一ページあたりの数)"]
	[return cond="sys:const.sn.bookmark[_sl.place] == null"]

	[enable_event enabled=false]
		[ask_ync_cleartext]
		[button enabled=false layer="mes_yesno" page=fore pic="& 'userdata:/storage/'+ _sl.place +'.jpg'" left=570 top=220]
		[ask_ync mes="クイックロードしますか？" no_cleartext=true]
			;#FONT	クイックロードしますか？
	[enable_event enabled=true]
	[return cond="_yesno != 'y'"]
[jump label=*do_load]


;*********************************************************
*exit
	[se fn=&sysse_cancel buf=SYS]

	[tsy_frame id=archive time=500 y=&const.sn.frm.archive.height]
	[wait_tsy id=archive]
	[frame id=archive visible=false]

	[enable_event enabled=true]
	[set_focus to=null]
	[let name="isGameState" text=true cond="呼び元=='Text'"]
[return]
