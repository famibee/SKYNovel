;#USE-FONT	ipamjm
	[add_lay layer=mes_submenu class=txt]
	[lay layer=mes_submenu b_pic=_submenu b_alpha=1 b_alpha_isfixed=true b_left=0 b_top=0 visible=false]
		[button layer=mes_submenu page=fore pic=_submenuBtn left=130 top=108 global=true label=*save hint="セーブ" enterse=&sysse_choice]
		[button layer=mes_submenu page=fore pic=_submenuBtn left=130 top=178 global=true label=*load hint="ロード" enterse=&sysse_choice]
		[button layer=mes_submenu page=fore pic=_submenuBtn left=130 top=248 global=true label=*log hint="文字履歴を表示" enterse=&sysse_choice]
		[button layer=mes_submenu page=fore pic=_submenuBtn left=130 top=318 global=true label=*config hint="ゲーム設定" enterse=&sysse_choice]
		[button layer=mes_submenu page=fore pic=_submenuBtn left=130 top=388 global=true label=*hide_text hint="テキスト窓非表示" enterse=&sysse_choice]
		[button layer=mes_submenu page=fore pic=_submenuBtn left=130 top=458 global=true label=*cancel hint="ゲームに戻る" enterse=&sysse_choice]
		[button layer=mes_submenu page=fore pic=_submenuBtn left=130 top=528 global=true label=*title hint="タイトルに戻る" enterse=&sysse_choice]
		[button layer=mes_submenu page=fore pic=_submenuBtn left=130 top=598 global=true label=*close hint="ゲーム終了" enterse=&sysse_choice cond=const.sn.isApp]

	[event global=true call=true key=rightclick label=*main]
	[event global=true call=true key=shift label=*main]
	[event global=true call=true key=escape label=*main]
;	[gesture_event global=true call=true f2tap=*main f3tap=*toggle_skip]
[return]

;*********************************************************
*main
	[return cond="! isGameState"]

		[se fn=&sysse_ok1 buf=SYS]
		[lay layer=mes_submenu page=fore visible=true]
		[let name=sysmenu_v text=&const.sn.lay.mes_sysmenu.fore.visible cond=useSysMenu]
		[sys_menu visible=false]

	[let name="isGameState" text=false]
*retry
	[enable_event layer=mes_submenu enabled=true]
	[enable_event enabled=false]
	[clear_event]

	[event key=rightclick label=*cancel]
	[event key=shift label=*cancel]
	[event key=escape label=*cancel]
	[set_focus to=null]
[s]

;*********************************************************
*toggle_skip
	[jump fn=sub label=*toggle_skip]

;*********************************************************
*save
	[se fn=&sysse_ok1 buf=SYS]
	[call label=*exit]
	[let name="isGameState" text=true]
	[jump fn="_archive" label=*rclick_save]

;*********************************************************
*load
	[se fn=&sysse_ok1 buf=SYS]
	[call label=*exit]
	[let name="isGameState" text=true]
	[jump fn="_archive" label=*rclick_load]

;*********************************************************
*config
	[se fn=&sysse_ok1 buf=SYS]
	[call label=*exit]
	[let name="isGameState" text=true]
	[jump fn="_config" label=*rclick_config]

;*********************************************************
*hide_text
	[se fn=&sysse_ok1 buf=SYS]
	[call label=*exit]
	[let name="isGameState" text=true]
	[jump fn="_hidetext" label=*main]

;*********************************************************
*log
	[se fn=&sysse_ok1 buf=SYS]
	[call label=*exit]
	[let name="isGameState" text=true]
	[jump fn="_log" label=*main]

;*********************************************************
*title
	[enable_event layer=mes_submenu enabled=false]
	[ask_ync mes="タイトルに戻りますか？"]
		;#FONT	タイトルに戻りますか？
	[jump label=*retry cond="_yesno != 'y'"]

	[call label=*exit]

	[fadeoutse buf=音声 time=1000]
	; テキストレイヤ全画面設定
	[txt_lay_fullscreen b_alpha=0]
	[grp bg="black" sys_menu=false b_alpha=0]

	[pop_stack clear=true]
	[jump fn="main" label=*title]

;*********************************************************
*close
	[enable_event layer=mes_submenu enabled=false]
	[ask_ync mes="ゲームを終了しますか？"]
		;#FONT	ゲームを終了しますか？
	[jump label=*retry cond="_yesno != 'y'"]

	[jump fn="main" label=*close_do]

;*********************************************************
*exit
	[lay layer=mes_submenu page=fore visible=false]
	[sys_menu visible=&sysmenu_v]
[return]

;*********************************************************
*cancel
	[se fn=&sysse_cancel buf=SYS]
	[lay layer=mes_submenu page=fore visible=false]
	[sys_menu visible=&sysmenu_v]
	[wait time=200]		; パフォーマンス改善

	[enable_event enabled=true]
	[let name="isGameState" text=true]
[return]
