;#USE-FONT	ipam
	[return cond="const.saveload.inited"]
	[let name="const.saveload.inited" text=true]

	[add_frame id=saveload src="saveload/_saveload.htm" visible=false]

	[event key='dom=saveload:#close' label=*exit global=true]
[return]

;*********************************************************
; 最新値で更新
*val2ctrl
[let_ml name=dic]
	[
		{"fn": "../bgimage/yun_1317.jpg", "dt": "2018/12/18 11:44", "txt": "　ああ、桜の樹の下には屍体が埋まつてゐる！"},
		{"fn": "../bgimage/yun_2384.jpg", "dt": "2018/12/18 11:43", "txt": "　この渓間ではなにも俺をよろこばすものはない。鶯《うぐひす》や四十雀《しじふから》も、白い日光をさ青に煙らせてゐる木の若芽も、ただそれだけでは、もうろうとした心象に過ぎない。俺には惨劇が必要なんだ。その平衡があつて、はじめて俺の心象は明確になつて来る。俺の心は悪鬼のやうに憂欝に渇いてゐる。俺の心に憂欝が完成するときにばかり、俺の心は和《なご》んで来る。"},
		{"fn": "../bgimage/yun_2352.jpg", "dt": "2018/12/18 11:42", "txt": "  This is not justified test message. This is written many words and cut last sentence."},
		{"fn": "../bgimage/yun_1317.jpg", "dt": "2018/12/18 11:41", "txt": "　それは渓の水が乾いた磧《かはら》へ、小さい水溜を残してゐる、その水のなかだつた。思ひがけない石油を流したやうな光彩が、一面に浮いてゐるのだ。お前はそれを何だつたと思ふ。それは何万匹とも数の知れない、薄羽かげらふの屍体だつたのだ。隙間なく水の面を被つてゐる、彼等のかさなりあつた翅《はね》が、光にちぢれて油のやうな光彩を流してゐるのだ。そこが、産卵を終つた彼等の墓場だつたのだ。"},
		{"fn": "../bgimage/yun_1184.jpg", "dt": "2018/12/18 11:40", "txt": "　二三日前、俺は、ここの渓へ下りて、石の上を伝ひ歩きしてゐた。水のしぶきのなかからは、あちらからもこちらからも、薄羽かげらふがアフロデイツトのやうに生れて来て、渓の空をめがけて舞ひ上つてゆくのが見えた。お前も知つてゐるとほり、彼等はそこで美しい結婚をするのだ。暫らく歩いてゐると、俺は変なものに出喰はした。"}
	]
[endlet_ml]
	[set_frame id=saveload var_name=val_dic text=&dic]
	[let_frame id=saveload var_name=val2ctrl function=true]
[return]

;*********************************************************
;	TitleMenu -> Load
*title_load
	[let name="しおりモード" text="Load"]
	[let name="呼び元" text="Title"]
[jump label=*main]

;*********************************************************
;	右クリック -> Load
*rclick_load
	[return cond="! isGameState"]

	[let name="しおりモード" text="Load"]
	[let name="呼び元" text="Text"]
[jump label=*main]

;*********************************************************
;	右クリック -> Save
*rclick_save
	[return cond="! isGameState"]

	[let name="しおりモード" text="Save"]
	[let name="呼び元" text="Text"]
[jump label=*main]

;*********************************************************
*main
	[enable_event enabled=false]
	[clear_event]

	; 最新値で更新
	[set_frame id=saveload var_name=val_mode text=&しおりモード]
	[set_frame id=saveload var_name=val_caller text=&呼び元]
	[call label=*val2ctrl]
	; 下部からスライドイン
	[frame id=saveload visible=true y=&const.sn.frm.saveload.height]
	[tsy_frame id=saveload time=500 y=0]
	[wait_tsy id=saveload]

	[event key='dom=saveload:#do_loadsave' label=*do_loadsave call=true]
	[event key='dom=saveload:#do_del' label=*do_del call=true]

	&isGameState = false
	[event key=rightclick label=*exit]
	[event key=command label=*exit]
	[event key=control label=*exit]
	[event key=end label=*exit]
	[event key=shift label=*exit]
	[event key=c label=*exit]
;	[gesture_event swipe=*nop f2tap=*exit f3tap=*exit]
	[set_focus to=null]
[s]


*do_loadsave
	[let_frame id=saveload var_name=val_place]
	[trace text=&const.sn.frm.saveload.val_place]
[return]

*do_del
	[let_frame id=saveload var_name=val_place]
	[trace text=&const.sn.frm.saveload.val_place]
[return]


*chg_page
	[let name="mark_p" text="&sys:saveload.page * const._sl.th_一ページあたりの数"]
	[call label=*btn_save cond="しおりモード == 'Save'"]
	[call label=*btn_load cond="しおりモード == 'Load'"]

	[button layer="_syslay.mes" text=" Return " style='fill: dodgerblue;' style_hover='fill: darkturquoise;' left=916 top=16 global=true label=*exit hint="戻る" enterse=&sysse_choice clickse=&sysse_cancel join=true]	; 戻るボタン

	[zoom_tsy layer="_syslay.bg" time=400 visible=true cond=is_init]
	&is_init = false

[s]

;*********************************************************
*save
	[let name="_sl.place" text="&sn.eventArg"]
	[let name="exsits" text="&sys:const.an.bookmark[_sl.place] != null"]
	[se fn=&sysse_ok1 buf=SYS]
	[jump label=*ask_end_save cond="! exsits"]

	[enable_event layer="_syslay.mes" enabled=false]
		[_sl.set_pos place=&_sl.place]
		[plugin name="Mnu_InfoThumb" visible=true left=&pos.x top=&pos.y float=true]

		[ask_ync mes="しおりを上書き保存しますか？"]
			;#FONT	しおりを上書き保存しますか？

		[plugin name="Mnu_InfoThumb" visible=false]
	[enable_event layer="_syslay.mes" enabled=true]
	[jump label=*retry cond="_yesno != 'y'"]

*ask_end_save
	[call label=*do_save]
[jump label=*exit]

*do_save
	[if exp="const.flash.system.Capabilities.playerType == 'Desktop'"]
		[snapshot fn="& 'app-storage:/bookmark/'+ _sl.place" layer=&dsp_lays width=&const._sl.サムネイル画像幅 height=&const._sl.サムネイル画像高さ quality=90 smoothing=true]
	[else]
		[snapshot fn="& 'ss:/'+ _sl.place" format="jpg" layer=&dsp_lays width=&const._sl.サムネイル画像幅 height=&const._sl.サムネイル画像高さ quality=90 smoothing=true]
	[endif]
*do_save_noss
	[let name="enabled" text="&save:const.an.layer.mes.enabled|true"]
	[enable_event enabled=true]	; この状態も保存されるので一時変更
	[save place=&_sl.place path="app-storage:/bookmark/"]
	[enable_event enabled="&enabled"]
[return]

*do_save_resume
	; 変数placeを変更しないよう注意。呼び元で使用している。
	[let name="enabled" text="&save:const.an.layer.mes.enabled|true"]
	[enable_event enabled=true]	; この状態も保存されるので一時変更
	[save place=99999]
	[enable_event enabled="&enabled"]
[return]

;*****************************
*quick_save
	[return cond="! isGameState"]

	[let name="_sl.place" text="&sys:saveload.quick_place"]
	[call label=*do_save]
	[let name="sys:saveload.quick_place" text="&(_sl.place +1) % const._sl.th_一ページあたりの数 +(const._sl.ページ数 *const._sl.th_一ページあたりの数)"]

	[notice text="クイックセーブしました" left="&const.flash.display.Stage.stageWidth -10 -300" top=10]
[return]

;*********************************************************
*load
	[let name="_sl.place" text="&sn.eventArg"]
	[jump label=*ask_end_load cond="呼び元 == 'Title'"]
	[se fn=&sysse_ok1 buf=SYS]

	[enable_event layer="_syslay.mes" enabled=false]
		[_sl.set_pos place=&_sl.place]
		[plugin name="Mnu_InfoThumb" visible=true left=&pos.x top=&pos.y float=true]

		[ask_ync mes="しおりを読み込みますか？"]
			;#FONT	しおりを読み込みますか？

		[plugin name="Mnu_InfoThumb" visible=false]
	[enable_event layer="_syslay.mes" enabled=true]
	[jump label=*retry cond="_yesno != 'y'"]
	[jump label=*end_load]

*ask_end_load
	[se fn=&sysse_ok2_long buf=SYS]
*end_load
	; *** ブラックアウト ***
	[clear_lay layer="_syslay.mes" page=back]
	[lay layer="_syslay.bg" page=back visible=false]
	[trans layer="_syslay.mes,_syslay.bg" time=200]

*do_load
	[sys_scenario_start no_resume_save=true]
	[copybookmark from=&_sl.place to=99999]
	[load place=&_sl.place]
;	[load place=&_sl.place fn=saveload label=*do_load_init]

*do_load_resume
	[se fn=&sysse_ok2_long buf=SYS]
;	[notice text="前回終了ポイントから再開します（キャンセル=ESC）" left="&const.flash.display.Stage.stageWidth -10 -300" top=10]
	[sys_scenario_start no_resume_save=true]
	[load place=99999]
;	[load place=99999 fn=saveload label=*do_load_init]

; ロード後最初にしたい共通処理
*do_load_init

[return]

;*****************************
*quick_load
	[return cond="! isGameState"]
	[return cond="sys:saveload.quick_place == null"]
	[let name="_sl.place" text="&(sys:saveload.quick_place +const._sl.th_一ページあたりの数 -1) % const._sl.th_一ページあたりの数 +(const._sl.ページ数 *const._sl.th_一ページあたりの数)"]
	[return cond="sys:const.an.bookmark[_sl.place] == null"]

	[enable_event enabled=false]
		[ask_ync_cleartext]
		[button enabled=false layer="mes_yesno" page=fore pic="& 'app-storage:/bookmark/'+ _sl.place +'.jpg'" left=570 top=220]
		[ask_ync mes="クイックロードしますか？" no_cleartext=true]
			;#FONT	クイックロードしますか？
	[enable_event enabled=true]
	[return cond="_yesno != 'y'"]
[jump label=*do_load]


;*********************************************************
*exit
	[se fn=&sysse_cancel buf=SYS]

	[tsy_frame id=saveload time=500 y=&const.sn.frm.saveload.height]
	[wait_tsy id=saveload]
	[frame id=saveload visible=false]

	[enable_event enabled=true]
	[set_focus to=null]
	[let name="isGameState" text=true cond="呼び元=='Text'"]
[return]
