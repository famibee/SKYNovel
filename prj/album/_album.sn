;#USE-FONT	ipam
	[return cond="const.sn.frm.album"]

	[add_frame id=album src="album/_album.htm" visible=false]

	; アルバム表示内容（ここをカスタマイズ。json形式で記述）
[let_ml name=dic]
	[
		{"type": "pic", "fn": "../bg/yun_1184.jpg", "title": "川辺"},
		{"type": "pic", "fn": "../bg/yun_1317.jpg", "title": "水中"},
		{"type": "pic", "fn": "../bg/yun_2352.jpg", "title": "桜1"},
		{"type": "pic", "fn": "../bg/yun_2384.jpg", "title": "桜2"},
		{"type": "pic", "fn": "../image/F_口元.jpg", "title": "語り手"},
		{"type": "pic", "fn": "_album_miken.jpg", "title": "？？？", "enabled": "false"},
		{"type": "mov", "fn": "nc10889.mp4", "title": "桜"},
		{"type": "bgm", "fn": "free32", "title": "free32(BGM)"},
		{"type": "bgm", "fn": "free49", "title": "free49(BGM)"},
		{"type": "bgm", "fn": "free61", "title": "free61(BGM)"},
		{"type": "bgm", "fn": "free0509", "title": "free0509(BGM)"}
	]
[endlet_ml]
	[set_frame id=album var_name=val_dic text=&dic]
	[let_frame id=album var_name=init function=true]

	[event key='dom=album:.bgm' label=*bgm global=true call=true]
	[event key='dom=album:#close' label=*exit global=true]


[macro name="アルバム解放"]
	[let name="&'sys:album.'+ mp:name" text=true]
[endmacro]
[return]


;*********************************************************
*title_album
	[enable_event enabled=false]
	[clear_event]

	; 最新値で更新
	[call label=*val2ctrl]
	; 下部からスライドイン
	[frame id=album visible=true y=&const.sn.frm.album.height]
	[tsy_frame id=album time=500 y=0]
	[wait_tsy id=album]

	[event key=rightclick label=*exit]
	[event key=command label=*exit]
	[event key=control label=*exit]
	[event key=end label=*exit]
	[event key=shift label=*exit]
;	[gesture_event f2tap=*exit f3tap=*exit]
	[set_focus to=null]
[s]

; 最新値で更新
*val2ctrl
	[set_frame id=album var_name=val_fnBgm text=&save:const.sn.fnBgm|'(No BGM)']
	[let_frame id=album var_name=val2ctrl function=true]
[return]

; BGM切り替え
*bgm
	[playbgm fn=&sn.event.domdata.arg]
	[call label=*val2ctrl]
[return]

;*********************************************************
*exit
	[se fn=&sysse_cancel buf=SYS]

	[tsy_frame id=album time=500 y=&const.sn.frm.album.height]
	[wait_tsy id=album]
	[frame id=album visible=false]

	[enable_event enabled=true]
	[set_focus to=null]
[return]
