;#USE-FONT	ipam
;	[loadplugin fn="ipam"]	; この行は触らないで下さい
;********************************************************
;	定数
;********************************************************
; 各種効果音を指定
&sysse_ok1 = "BurstB_11"
&sysse_ok2 = "BellA_16"
&sysse_ok2_long = "BellB_11"
&sysse_cancel = "bell05"
&sysse_choice = "wood04"

; メッセージウインドウにボタンを配置するか（システムメニュー）
&useSysMenu = true

; 立ち絵配置位置定数
;	[lay pos=&pos.l	等として使用。
;	組み込みで「c」という指定が出来る。例：pos=c、pos="c"
;	これを指定すると画面中央。（pos、leftを省略するとpos=c扱い）
&pos = 140
&pos.c = const.sn.config.window.width /2
&pos.r = const.sn.config.window.width -pos.l
&pos.lc = (pos.c + pos.l) /2
&pos.rc = (pos.r + pos.c) /2

; 「桜の樹の下には」特有の立ち絵配置位置定数
&pos.vleft_disp_x = 40 + 280
&pos.vleft_disp_w = const.sn.config.window.width - pos.vleft_disp_x

&pos.l1c = pos.vleft_disp_x + pos.vleft_disp_w * (1/2)

&pos.l2l = pos.vleft_disp_x + pos.vleft_disp_w * (1/4)
&pos.l2r = pos.vleft_disp_x + pos.vleft_disp_w * (3/4)

; 表示されるレイヤ（セーブ・スナップショットなどの対象）
&dsp_lays_grp = "base,0,1,2"	; 画像
&dsp_lays = dsp_lays_grp +',mes'	; 文字
[let name="dsp_lays" text="&dsp_lays_grp +',mes,mes_sysmenu'" cond=useSysMenu]

;********************************************************
;	各種初期値
;********************************************************
[if exp=const.sn.isFirstBoot]	; ゲームがインストールされ、初めての起動
	&sys:TextLayer.Back.Alpha = 0.5
		; バック不透明度。テキストウインドウの背景の濃度
[endif]

;********************************************************
; マクロ タイトル開始共通処理（main.snで呼ぶ）
;********************************************************
[macro name=sys_title_start]
	&lastVoice = ""
	&isGameState = false
	&sys:doResume = false
	&save:sn.doRecLog = false

	[txt_lay_fullscreen b_alpha=0 onend=leave no_sysmenu_draw=true]
	[er]	; ページ両面の文字消去＆改ページ
		; （改ページしないと履歴がページからあふれる）
	[set_focus to=null]
	[fadeoutse buf="SE" time=500]
	[sys_menu visible=false]
	[clear_event]
[endmacro]

;********************************************************
; マクロ シナリオ開始時共通処理（ロード時にもコール）
;********************************************************
[macro name=sys_scenario_start]
	&lastVoice = ""
	&isGameState = true
	&save:sn.doRecLog = true

	[fadeoutbgm time=300]
	[fadeoutse buf="SE" time=500]

	; テキストレイヤ全画面設定
	[txt_lay_fullscreen b_alpha=0 no_sysmenu_draw=true]
	[lay layer="mes" page=back layout='fontFamily="ipam" fontLookup="embeddedCFF"']
	[grp bg="black" sys_menu=false b_alpha=0 time=0]

	[record_place layer=&dsp_lays]
		[sys_resume_save cond="! sys:doResume && ! no_resume_save"]
		&sys:doResume = true

[jump label=*game_inited cond="const.game.inited"]
&const.game.inited = true

	[call fn=extG_*]	; extG_*.snというスクリプトを全部コールする
	[call fn="_hidetext"]
	[call fn="_submenu"]
	[call fn="_archive"]
	[call fn="_log"]
	[call fn="_config"]
	[call fn="_album"]

	[event global=true call=true key=a label=*toggle_auto_read]
	[event global=true call=true key=f label=*toggle_skip]

*game_inited
	[enable_event enabled=true]
	[set_focus to=null]
[endmacro]

;********************************************************
; マクロ レジューム処理
[macro name=sys_resume_load]
	[jump fn=_archive label=*do_load_resume cond=sys:doResume]
[endmacro]

;********************************************************
; マクロ レジューム退避処理
[macro name=sys_resume_save]
	[call fn=_archive label=*do_save_resume]
[endmacro]


;********************************************************
; マクロ システムメニュー描画処理
[macro name=sysmenu_draw]
	[return cond=!useSysMenu]
	[sys_menu visible=false]
	[clear_lay layer="mes_sysmenu" page=both]
	[return cond="mp:no_sysmenu_draw != null"]

	[let name=l text=%l|40]
	[let name=w text=%w|292]
	[let name=sysmenu_left text=&l-18]
	[let name=sysmenu_left2 text=&sysmenu_left+w+13]
;	&r = 0							; ボタンを横にしたいなら
	&r = -90 *const.sn.Math.PI /180	; ボタンを縦にしたいなら

	[lay layer="mes_sysmenu" page=back]
	[button layer="mes_sysmenu" page=back text="クイックセーブ" rotation=&r left=&sysmenu_left2 top=600 global=true event_at_down=true fn="_archive" label=*quick_save call=true hint="クイックセーブ" enterse=&sysse_choice]
	[button layer="mes_sysmenu" page=back text="クイックロード" rotation=&r left=&sysmenu_left2 top=720 global=true event_at_down=true fn="_archive" label=*quick_load call=true hint="クイックロード" enterse=&sysse_choice]

	[button layer="mes_sysmenu" page=back text="タイトルへ" rotation=&r left=&sysmenu_left top=220 global=true event_at_down=true fn="sub" label=*title_sys call=true hint="タイトルへ" enterse=&sysse_choice]
		; どの本文スクリプトから呼ばれるか不明なので、
		; 同一スクリプトへのコールだがfn属性を省略してはいけない

	[button layer="mes_sysmenu" page=back text=" セーブ " rotation=&r left=&sysmenu_left top=320 global=true event_at_down=true fn="_archive" label=*rclick_save call=true hint="セーブ" enterse=&sysse_choice]
	[button layer="mes_sysmenu" page=back text=" ロード " rotation=&r left=&sysmenu_left top=420 global=true event_at_down=true fn="_archive" label=*rclick_load call=true hint="ロード" enterse=&sysse_choice]
	[button layer="mes_sysmenu" page=back text="字を隠す" rotation=&r left=&sysmenu_left top=520 global=true event_at_down=true fn=_hidetext label=*main call=true hint="文字非表示" enterse=&sysse_choice]
	[button layer="mes_sysmenu" page=back text=" 履 歴 " rotation=&r left=&sysmenu_left top=620 global=true event_at_down=true fn=_log label=*main call=true hint="履歴を表示" enterse=&sysse_choice]
	[button layer="mes_sysmenu" page=back text=" 設 定 " rotation=&r left=&sysmenu_left top=720 global=true event_at_down=true fn=_config label=*keycall_config call=true hint="ゲーム設定" enterse=&sysse_choice]
	[trans layer="mes_sysmenu" time=0]
	[wt]	; 裏から表へコピー（save対策）
[endmacro]

;-----------------------------
; マクロ システムメニュー表示切り替え
[macro name=sys_menu]
	[lay layer="mes_sysmenu" visible=%visible|true cond=useSysMenu]
[endmacro]

[add_lay layer="mes_sysmenu" class=txt cond=useSysMenu]
[let name=sysmenu_left text=NaN]

;********************************************************
; マクロ テキストレイヤ縦書き左設定
[macro name="txt_lay_v_left"]
	[let name=l text=%l|40]
	[let name=w text=%w|292]
;	&pl = l +26
	&pl = 26
	&pt = 66
;	&pr = const.sn.config.window.width -(l +w) +26
	&pr = 26
	&pb = 66
	[let name=fcol text=%fcol|'white']
	; こちらの素材をお借りしました。
	; びたちー素材館　メッセージボックス・メッセージウィンドウ・フレーム　フリー素材 http://www.vita-chi.net/message.htm
	[lay * layer=%layer|'mes' page=%page|back visible=%visible|true left=&l top=0
		b_pic=%b_pic|wafuu1	; 背後に画像を使う
	;	b_color=%b_color|'#ffffff'	; 背後に画像を使わない
		b_alpha=%b_alpha|1
	;	b_alpha_isfixed=true	; コメント外すと透過度変更禁止
		style="&'color: #{fcol}; width: #{w}px; height: #{const.sn.config.window.height}px; writing-mode: vertical-rl; padding-left: #{pl}px; padding-right: #{pr}px; padding-top: #{pt}px; padding-bottom: #{pb}px; font-size: 24px; line-height: 1.5;'"]
		; r_color=&fcol r_size=12 max_col=25 bura_col=2 max_row=%max_row|7 onsplit="enter"
		; 縦書きにしたいなら writing-mode を vertical-rl に。横書きは horizontal-tb
	[sysmenu_draw *]
[endmacro]

;********************************************************
; マクロ テキストレイヤ縦書き中設定
[macro name="txt_lay_v_center"]
	[txt_lay_v_left * l=%l|366]
[endmacro]

;********************************************************
; マクロ テキストレイヤ縦書きWide中設定
[macro name="txt_lay_v_center_wide"]
	[txt_lay_v_center * l=%l|294 w=436 max_row=11
	b_pic=wafuu1w	; 背後に画像を使わない場合はコメントアウト
]
[endmacro]

;********************************************************
; マクロ テキストレイヤ全画面設定
[macro name="txt_lay_fullscreen"]
	[let name=t text=%t|40]
	[let name=h text=%h|'&const.sn.config.window.height -40*2']
	&pl = 70
	&pt = t +30
	&pr = 70
	&pb = 70
	[let name=fcol text=%fcol|0x000000]
	[lay * layer=%layer|"mes" page=%page|back visible=%visible|true b_left=0 b_top=&t-20 left=0 top=0
	;	b_pic=%b_pic|hakkou1
			; 背後に画像を使う場合
		b_width="&const.sn.config.window.width -40*2" b_height=&h b_color=%b_color|0x000000
			; 背後に画像を使わない場合
		b_alpha=%b_alpha|1 r_color=&fcol r_size=12 max_col=35 bura_col=2 max_row=%max_row|18 onsplit="enter" layout=#&'lineHeight="36" justificationRule="space" columnGap="0" paddingLeft="$pl" paddingTop="$pt" paddingRight="$pr" paddingBottom="$pb" verticalAlign="inherit" blockProgression="tb" lineBreak="explicit" fontLookup="embeddedCFF" renderingMode="cff" fontSize="24" locale="ja" kerning="off" trackingRight="0" color="$fcol" whiteSpaceCollapse="preserve"'#]

	[sysmenu_draw *]
[endmacro]

;********************************************************
; マクロ テキストレイヤ横書きWide下設定
[macro name="txt_lay_h_bottom_wide"]
	[txt_lay_fullscreen * t=%t|472 h=266 max_row=6]
[endmacro]

;********************************************************
; マクロ ＢＧＭ切り替え
[macro name="bgm"]
	[stopbgm cond="mp:time == null || sn.skip.enabled"]

	[let name=t text=%time|500]	;省略した時の初期値
	[stopse buf=BGM_FO]
	[xchgbuf buf=BGM buf2=BGM_FO]

	[if exp="mp:time == null || sn.skip.enabled"]
		[playbgm *]
	[else]
		[fadeoutse buf=BGM_FO time=&t]
		[playbgm * volume=0]
		[fadebgm * volume=%volume|1 time=&t]
	[endif]
[endmacro]

;********************************************************
; マクロ 効果音
[macro name="se"]
	[stopse * cond="mp:fn == null"]
	[playse * cond="mp:fn != null" join=false]
[endmacro]

;********************************************************
; マクロ 音声履歴開始
[macro name="h_voice"]
	[rec_ch text=#&'｜　《link｜{"fn":"sub","label":"*h_voice","call":"true","global":"true","arg":"$lastVoice"}》'# r=false]
[endmacro]

;********************************************************
; マクロ 音声履歴終了
[macro name="end_h_voice"]
	[rec_ch text="｜　《endlink｜》" r=false]
[endmacro]

;********************************************************
; マクロ 改ページ
[macro name="plc"]
	[ws buf="音声" canskip=true cond="sn.auto.enabled && !sn.skip.enabled"]
	[p visible=%visible|true]
	[er]
	[record_place layer=&dsp_lays]
		[sys_resume_save]
	&lastVoice = ""
	[fadeoutse buf="SE" time=%time|500 cond="mp:no_se_stop === null && !sn.skip.enabled"]
	[stopse buf="SE" cond="sn.skip.enabled"]
	[stopse buf=%buf|音声 cond="mp:no_voice_stop === null || sn.skip.enabled"]
[endmacro]

;********************************************************
; マクロ 選択肢直後にすべき処理
[macro name="after_choice"]
;	[pop_stack]
	[record_place layer=&dsp_lays]
		[sys_resume_save]
[endmacro]

;********************************************************
; マクロ 履歴画面で選択位置から再開する
&const.h_save.max_count = 50
[macro name="h_save"]
	[未作成]

	[let name="h_save_idx" text="0" cond="h_save_idx == null"]

	[let name="_sl.place" text="&'h_'+ h_save_idx"]
	[call fn=_archive label=*do_save_noss]

	[rec_ch text=#&'｜　《grp｜｜{"pic":"app-storage:/bookmark/'+ _sl.place +'.jpg","width":"198","height":"149"}》'# r=false]
	[rec_ch text=#&'｜　《link｜{"fn":"sub","label":"*h_jmp","call":"true","global":"true","arg":"$_sl.place"}》'# r=false]
	[rec_ch text="【このページから再開】"]
	[rec_r][rec_r]

	[rec_ch text="｜　《endlink｜》" r=false]
	[let name="h_ss_idx" text="&h_save_idx %const.h_save.max_count"]
	[let name="h_save_idx" text="&(h_save_idx +1) %const.h_save.max_count"]
[endmacro]

;********************************************************
; マクロ 場面転換
;********************************************************
[macro name="grp"]
	[let name=t text=%t_time|300]	;省略した時の初期値
	[let name=t text=0 cond="sn.skip.enabled"]
	[let name=tt text=%time|1000]	;省略した時の初期値
	[let name=tt text=0 cond="sn.skip.enabled"]

	; テキストOff
	[if exp="mp:nofo_txt == null"]
		[sys_menu visible=false]
		[lay layer=mes page=back visible=false]
		[trans layer=mes time=&t]
		[wt]
		[er rec_page_break=false]
	[endif]

	; 効果音
	[se fn=%se cond="mp:se != null"]

	; レイヤ切り替え・メイン
	[lay layer=base page=back fn=%bg scale_x=%sxb|null face=%fb|null visible=true cond="mp:bg != null"]
	[img layer=0 fn=%l0|null pos=%pos0|null left=%left0|null top=%top0|null alpha=%o0|null scale_x=%sx0|null face=%f0|null]
	[img layer=1 fn=%l1|null pos=%pos1|null left=%left1|null top=%top1|null alpha=%o1|null scale_x=%sx1|null face=%f1|null]
	[img layer=2 fn=%l2|null pos=%pos2|null left=%left2|null top=%top2|null alpha=%o2|null scale_x=%sx2|null face=%f2|null]
	[trans * layer=&dsp_lays_grp time=&tt]
	[wt]

	; テキストOn
	[if exp="mp:nofi_txt == null"]
		[lay layer=mes page=back visible=true b_alpha=%b_alpha|1]
		[trans layer=mes time=&t]
		[wt]
		[sys_menu visible=%sys_menu|true]
	[endif]
[endmacro]

;********************************************************
; マクロ 文字数分のウェイトを入れる
[macro name="wc"]
	[wait cond="mp:time != null && sys:sn.tagCh.doWait && !sn.skip.enabled" time="&mp:time * sys:sn.tagCh.msecWait"]
[endmacro]



;********************************************************
[return]

;********************************************************
*toggle_auto_read
	&tmp:sn.auto.enabled = true
	[set_cancel_skip]
	[notice text='自動読み進みOn']
	[return]

*toggle_skip
	&tmp:sn.skip.enabled = true
	[set_cancel_skip]
	[notice text='強制スキップ On']
	[return]

*h_voice
	[voice fn=&const.sn.eventArg]
	[return]

*h_jmp
	&_sl.place = const.sn.eventArg

	[log_let_page name=h_page]
	[log_let_page_count name=h_page_count]
	[notice text='容量制限により、これ以前で再開できません' cond="h_page_count -h_page > const.h_save.max_count"]
	[return cond="h_page_count -h_page > const.h_save.max_count"]

		[ask_ync_cleartext]
		[button enabled=false layer="mes_yesno" page=fore pic="& 'app-storage:/bookmark/'+ _sl.place +'.jpg'" left=570 top=220]
		[ask_ync mes="ここから再開しますか？" no_cleartext=true]
			;#FONT	ここから再開しますか？
	[return cond="_yesno != 'y'"]

	[call fn=_log label=*cancel]
	[jump fn=_archive label=*end_load]

;********************************************************
*title_sys
	[enable_event enabled=false]
	[enable_event layer="mes_sysmenu" enabled=false]
	[ask_ync mes="タイトルに戻りますか？"]
		;#FONT	タイトルに戻りますか？
	[enable_event enabled=true]
	[enable_event layer="mes_sysmenu" enabled=true]
	[return cond="_yesno != 'y'"]

	[fadeoutse buf=音声 time=1000]
	; テキストレイヤ全画面設定
	[txt_lay_fullscreen b_alpha=0]
	[grp bg="black" sys_menu=false b_alpha=0]

	[pop_stack clear=true]
	[jump fn="main" label=*title]
